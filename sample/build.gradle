/*
 * Copyright (c) 2018, Nordic Semiconductor
 *
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.application'
apply from: rootProject.file("gradle/git-tag-version.gradle")

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "no.nordicsemi.android.nrfconnectdevicemanager"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode getVersionCodeFromTags()
        versionName getVersionNameFromTags()
        resConfigs 'en'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('../keystore')
            storePassword System.env.KEYSTORE_PSWD
            keyAlias System.env.KEYSTORE_ALIAS
            keyPassword System.env.KEYSTORE_KEY_PSWD
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity:1.6.1'
    implementation 'androidx.fragment:fragment:1.5.5'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'com.google.android.material:material:1.8.0-alpha03'
    // This is added to fix dependency issue with androidx.preference:preference:1.2.0,
    // which for some reason depends on androidx.fragment:fragment-ktx:1.3.6.
    implementation 'androidx.fragment:fragment-ktx:1.5.5'

    // Dagger 2
    implementation 'com.google.dagger:dagger:2.44.2'
    implementation 'com.google.dagger:dagger-android:2.44.2'
    implementation 'com.google.dagger:dagger-android-support:2.44.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.44.2'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.44.2'

    // Brings the new BluetoothLeScanner API to older platforms
    implementation 'no.nordicsemi.android.support.v18:scanner:1.6.0'

    // Timber & SLF4J
    implementation 'com.arcao:slf4j-timber:3.1'
    implementation 'no.nordicsemi.android:log-timber:2.3.0'

    // Mcu Mgr
    implementation project(':mcumgr-ble')

    // GSON
    implementation 'com.google.code.gson:gson:2.9.1'

    // Test
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.1'
    testImplementation 'junit:junit:4.13.2'
}
